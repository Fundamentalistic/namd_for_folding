#!/bin/csh -f
goto begin
syntax:
  echo ''
  echo 'Usage: config [debug] [tcl] [fftw] [<build_dir>/]<arch> '
  echo ''
  echo 'arch: ' `( cd arch ; ls -1 *.arch |sed -e 's/\.arch//' | egrep -v 'template' )`
  echo ''
  exit 1

exists:
  echo ''
  echo 'directory already exists'
  echo ''
  exit 1

begin:

  if ($#argv < 1) goto syntax
  if ( $1 == debug ) then
    set use_debug = 1
    shift
  else
    set use_debug = 0
  endif

  if ($#argv < 1) goto syntax
  if ( $1 == tcl ) then
    set use_tcl = 1
    shift
  else
    set use_tcl = 0
  endif

  if ($#argv < 1) goto syntax
  if ( $1 == fftw ) then
    set use_fftw = 1
    shift
  else
    set use_fftw = 0
  endif

  if ($#argv < 1) goto syntax
  set ARCH=$1 ; shift
  set DIR=`pwd`;

  if ( x$ARCH == x ) goto syntax
  if ( ! -f arch/$ARCH:t.arch ) goto syntax
  if ( -d $ARCH ) goto exists

  echo "Creating Directory: $ARCH"
  mkdir $ARCH
  cd $ARCH

  set ROOTDIR=$DIR
  if ( $ARCH:t == $ARCH ) set ROOTDIR='..'

  echo "Creating link: $ROOTDIR to .rootdir"
  ln -s $ROOTDIR .rootdir
  echo "Creating Makearch"
  echo include .rootdir/Make.charm >! Makearch
  echo include .rootdir/arch/$ARCH:t.arch >> Makearch
  echo 'CHARM = $(CHARMBASE)/$(CHARMARCH)' >> Makearch
  if ( $ARCH:t =~ *-MPI* ) then
    echo 'NAMD_PLATFORM = $(NAMD_ARCH)-MPI' >> Makearch
  else
    echo 'NAMD_PLATFORM = $(NAMD_ARCH)' >> Makearch
  endif
  echo 'include .rootdir/arch/$(NAMD_ARCH).base' >> Makearch
  if ( $use_tcl ) echo 'include .rootdir/arch/$(NAMD_ARCH).tcl' >> Makearch
  if ( $use_fftw ) echo 'include .rootdir/arch/$(NAMD_ARCH).fftw' >> Makearch
  if ( $use_debug ) then
    echo 'CXXOPTS = -g' >> Makearch
    echo 'CXXTHREADOPTS = -g' >> Makearch
    echo 'CXXSIMPARAMOPTS = -g' >> Makearch
    echo 'COPTS = -g' >> Makearch
  endif
  echo "Linking Makefiles"
  ln -s .rootdir/Makefile ./Makefile
  echo "Linking src"
  ln -s .rootdir/src ./src
  echo "Creating fftw and linking files"
  (cd $DIR/fftw;find . -name CVS -prune -o -type d -print )|sed -e s@.@@> temp.d
  (cd $DIR/fftw;find . -name CVS -prune -o -type f -print )|sed -e s@.@@> temp.f
  awk '{print "mkdir fftw"$1;}' < temp.d > temp.m
  awk '{print "ln -s ../.rootdir fftw"$1;}' < temp.d >> temp.m
  awk '{print "ln -s .rootdir/fftw"$1" fftw"$1;}' < temp.f > temp.l
  sh temp.m
  sh temp.l
  rm -f temp.d temp.f temp.m temp.l
  echo "Creating dpme2 and linking files"
  (cd $DIR/dpme2;find . -name CVS -prune -o -type d -print )|sed -e s@.@@> temp.d
  (cd $DIR/dpme2;find . -name CVS -prune -o -type f -print )|sed -e s@.@@> temp.f
  awk '{print "mkdir dpme2"$1;}' < temp.d > temp.m
  awk '{print "ln -s ../.rootdir dpme2"$1;}' < temp.d >> temp.m
  awk '{print "ln -s .rootdir/dpme2"$1" dpme2"$1;}' < temp.f > temp.l
  sh temp.m
  sh temp.l
  rm -f temp.d temp.f temp.m temp.l
  echo "Creating dpmta-2.6 and linking files"
  (cd $DIR/dpmta-2.6;find . -name CVS -prune -o -type d -print )|sed -e s@.@@> temp.d
  (cd $DIR/dpmta-2.6;find . -name CVS -prune -o -type f -print )|sed -e s@.@@> temp.f
  awk '{print "mkdir dpmta-2.6"$1;}' < temp.d > temp.m
  awk '{print "ln -s ../.rootdir dpmta-2.6"$1;}' < temp.d >> temp.m
  awk '{print "ln -s .rootdir/dpmta-2.6"$1" dpmta-2.6"$1;}' < temp.f > temp.l
  sh temp.m
  sh temp.l
  rm -f temp.d temp.f temp.m temp.l
  echo "You are ready to do a make in directory $ARCH now."
